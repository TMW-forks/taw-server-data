907-1.gat,47,38,0	script	Ligonn	929, {
	if (Forest_axe == 1 && countitem("Rope")>0 && countitem("Branch")>0) goto L_axe1;  
	if (Ligonn_know == 1) goto L_know;

	mes "[Ligonn]";
	mes "Hello! Do you need anything?";
	menu
		"Who are you?", L_who,
		"Do you know, how I could get out of this island?", -,
		"Nothing", L_No;

	mes "[Ligonn]";
	mes "As I remember when I was young, I have left a boat there, but you should repair it at first, I think it's too old.";
	next;
	mes "Please, don't ask me how. I didn't see it, so do it yourself.";
	next;
	close;

L_who:
	mes "[Ligonn]";
	mes "I'm Ligonn, the woodworker. I can make some wooden stuff for you, if you will need a help.";
	next;
	mes "Come back when you know what you need.";
	next;
	set Ligonn_know, 1;
	close;

L_axe1:
	mes "[Ligonn]";
	mes "I see you have everything you need for the axe. Wait a minute.";
	next;
	delitem "Rope", 1;
	delitem "Branch", 1;
	getitem "ForestAxe", 1;
	set Forest_axe, 5;
	mes "[Ligonn]";
	mes "Cutting trees should be simplier with this axe. Good luck.";
	close;

L_know:

	mes "[Ligonn]";
	mes "Hello! Do you need anything?";
	menu
		"I need wooden plank", L_plank,
		"I would need a paddle", L_paddle,
		"Do you know, how I could get out of this island?", -,
		"Cutting trees is very hard...", L_trees,
		"Do you need a help?", L_help,
		"Nothing", L_No;

	mes "[Ligonn]";
	mes "As I remember when I was young, I have left a boat there, but you should repair it at first, I think it's too old.";
	next;
	mes "Please, don't ask me how. I didn't see it, so do it yourself.";
	next;
	close;

L_help:
	if(countitem("nettlechaps")<1) goto L_nohelp;
	if(ligonn_help == 1) goto L_nohelp;
	mes "[Ligonn]";
	mes "Actually, If you helped me to collect supplies, I could improve your pants.";
	next;
	mes "[Ligonn]";
	mes "Give me your pants and 2 birch skins, 2 boar skins, 2 nettle string and a rope.";
	menu
		"I have what you need. Here.", -,
		"Okay, I will come later.", L_No;

	if(countitem("birchskin")<2) goto L_nohelp2;
	if(countitem("boarskin")<2) goto L_nohelp2;
	if(countitem("nettlestring")<2) goto L_nohelp2;
	if(countitem("rope")<1) goto L_nohelp2;
	if(countitem("nettlechaps")<1) goto L_nohelp2;
	mes "[Ligonn]";
	mes "This pants should be more durable.";
	next;
	delitem "birchskin", 2;
	delitem "boarskin", 2;
	delitem "nettlestring", 2;
	delitem "rope", 1;
	delitem "nettlechaps", 1;
	getitem "birchpants", 1;
	getexp 40, 10;
	close;
	
L_nohelp:
	mes "[Ligonn]";
	mes "Thanks, but I don't see anything you could help me with.";
	close;

L_nohelp2:
	mes "[Ligonn]";
	mes "Nonono... you still miss something. See you later.";
	close;


L_trees:
	if(Forest_axe==5) goto L_axe;
	mes "[Ligonn]";
	mes "Ah yes... cutting trees with knife must be hard.";
	next;
	mes "[Ligonn]";
	mes "I could make you an axe, if you bring me a branch and a rope. I will give you the rest.";
	set Forest_axe, 1;
	close;

L_axe:
	mes "[Ligonn]";
	mes "I already gave you an axe. You can try to improve your strenght.";
	close;

L_plank:
	if (countitem("log") > 0) goto L_plank_G;
	mes "[Ligonn]";
	mes "Okay, I can make 2 planks from 1 log. Bring me a log and I'll saw it for you.";
	close;

L_plank_G:
	mes "[Ligonn]";
	mes "Okay, do you want me to saw your log to two planks?";
	menu
		"Yes", -,
		"Nope", L_No;

	mes "Here you are.";
	delitem "log", 1;
	getitem "plank", 2;
	next;
	close;

L_No:
	close;
	
L_paddle:
	if (countitem("branch") > 0 && countitem("birchskin") > 0 && countitem("rope") > 0) goto L_paddle_G;
	mes "[Ligonn]";
	mes "Ah, I guess you are in hurry. I could make you simple paddle from branch, birch skin and rope. Bring me those three items.";
	close;
	
L_paddle_G:
	mes "[Ligonn]";
	mes "You have branch, birch skin and rope. I could make you a paddle, do you like it?";
	menu
		"Yes", -,
		"Nope", L_No;

	mes "It was honour for me to help you.";
	delitem "branch", 1;
	delitem "birchskin", 1;
	delitem "rope", 1;
	getitem "paddle", 1;
	next;
	close;
}
