//

907-1.gat,98,27,0	script	Broken Boat	127, {
	if(starting_tar == 1) goto L_Tar2;
	if(Start_leaving == 4) goto L_Tar;
	if(Start_leaving == 3) goto L_Leaving;
	if(Start_leaving == 2) goto L_Paddle;
	if(Start_leaving == 1) goto L_Planks;

	mes "-Broken Boat-";
	mes "You have found a boat, standing on the ground near water. You got an idea, that this boat can help you with driving away.";
	next;
	mes "What would you like to do?";
	menu
		"Look what is inside", -,
		"Nothing", L_no;

	mes "-Broken Boat-";
	mes "You are looking inside and you see, that it needs to be repaired.";
	next;
	mes "You found out, that to repair it, you need to get 3 planks.";
	set QUEST_BB_state, 1;
	set Start_leaving,1;
	next;
	close;

L_no:
	close;

L_Planks:
	if (countitem("plank") < 3) goto L_NotEnough;
	mes "-Broken Boat-";
	mes "You have brought 3 planks and tryed to repair it.";
	next;
	mes "You will need also a Paddle, if you want to leave. Get it!";
	set QUEST_BB_state, 2;
	set Start_leaving,2;
	delitem "plank", 3;
	next;
	close;
	

L_NotEnough:
	mes "-Broken Boat-";
	mes "Sorry, but it looks like you need some more planks. Three should be enough.";
	next;
	close;

L_Paddle:
	if (countitem("paddle") < 1) goto L_NotEnough2;
	mes "-Broken Boat-";
	mes "You understood that your idea is becoming really aviable.";
	next;
	mes "But to realise it, you need to get some food: 5 bottles of water and 3 dryed meat. You should also have a knife.";
	set Start_leaving,3;
	delitem "paddle", 1;
	next;
	close;

L_NotEnough2:
	mes "-Broken Boat-";
	mes "Read this more carefully: PADDLE. Get it!";
	next;
	close;

L_Leaving:
	if (countitem("bottleofwater") < 5) goto L_NotEnough3;
	if (countitem("dryedmeat") < 3) goto L_NotEnough3;
	if (countitem("knife") < 1) goto L_NotEnough3;
	mes "-Broken Boat-";
	mes "You have brought everything you need to leave this island.";
	next;
	mes "When you got into this boat you found letter from Ligonn.";
	next;
	mes "Letter:";
	mes "\"Hello, I wish you succesfull journey from the island. You should not forget to tar the boat as well - it will protect it from water. I will miss you. You will need some luck to survive on wild sea. This ring has worn my father every day he sailed to catch some fish. It brough him luck. I hope it will bring luck to you as well. Farewell!\"";
	next;
	delitem "BottleOfWater", 5;
	delitem "DryedMeat", 3;
	getitem "SailorsRing", 1;
	getexp 50, 10;
	set Start_leaving, 4;
	menu
		"Tar the boat before leaving", L_no,
		"Leave island without painting the boat", -;
	goto L_llleave;

L_llleave:
	mes "Are you sure you want to leave? Remember that you will never be able to return!";
	menu
		"Yes", -,
		"No", L_no;
	mes "You were sailing and sailing as you finally saw a land.";
	next;
	if(countitem("nettleshirt")>0 && countitem("birchpants")>0 && countitem("forestaxe")>0 && starting_tar == 1) getitem "BeginnersGuardian", 1;
	if(countitem("nettleshirt")>0 && countitem("nettlechaps")>0 && countitem("forestaxe")>0 && countitem("BeginnersGuardian")==0) getitem "BeginnersProtector", 1;
	warp "004-1.gat", 40, 44;
	mes "This is your new home.";
	savepoint "004-1.gat", 40, 44;	
	close;

L_NotEnough3:
	mes "-Broken Boat-";
	mes "You can be starwing while sailing, so get some supplies to survive: 5 bottles of water, 3 dryed meat and a knife.";
	close;

L_NotEnough4:
	mes "-Broken Boat-";
	mes "You don't have any tar!";
	close;

L_Tar:
	mes "-Broken Boat-";
	mes "Tar the boat before leaving?";
	menu
		"Yes", -,
		"Leave island without painting the boat", L_llleave;

	if (countitem("tar") < 1) goto L_NotEnough4;
	mes "After an hour of hard work the boat is finally protected.";
	next;
	getexp 40, 10;
	delitem "tar", 1;
	set starting_tar, 1;
	goto L_Tar2;

L_Tar2:
	mes "It's time to sail away!";
	next;
	goto L_llleave;
}
